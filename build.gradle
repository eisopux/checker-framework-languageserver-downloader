plugins {
    id 'java'
    id 'application'
    // https://plugins.gradle.org/plugin/com.gradleup.shadow
    id 'com.gradleup.shadow' version '8.3.6'
    id 'com.diffplug.spotless' version '7.0.3'
    id 'net.ltgt.errorprone' version '4.1.0'
}

group = 'io.github.eisopux'
version = '0.3.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'commons-io:commons-io:2.19.0'
    implementation 'net.lingala.zip4j:zip4j:2.11.5'
    implementation 'com.google.code.gson:gson:2.13.1'
    implementation 'commons-cli:commons-cli:1.9.0'

    testImplementation 'junit:junit:4.13.2'

    if (JavaVersion.current() >= JavaVersion.VERSION_17) {
        errorprone('com.google.errorprone:error_prone_core:2.37.0')
        errorproneJavac('com.google.errorprone:javac:9+181-r4173-1')
    }
}

application {
    // Define the main class for the application
    mainClass = 'org.checkerframework.languageserver.DownloaderMain'
}

shadowJar {
    minimize()
}

tasks.withType(JavaCompile) { compilationTask ->
    options.compilerArgs += ['-Xlint', '-Werror']
    options.errorprone.enabled = JavaVersion.current() >= JavaVersion.VERSION_17
}

spotless {
    java {
        googleJavaFormat().aosp()
        importOrder('com', 'jdk', 'lib', 'lombok', 'org', 'java', 'javax')
        formatAnnotations()
    }
    groovyGradle {
        target '*.gradle'
        importOrder()
        greclipse()  // which formatter Spotless should use to format .gradle files.
        leadingTabsToSpaces(4)
        trimTrailingWhitespace()
    }
}
